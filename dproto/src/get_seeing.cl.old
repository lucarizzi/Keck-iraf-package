#-----------------------------------------------------------------------
procedure seeing( image)
#-----------------------------------------------------------------------
string	image="last"	{ prompt="Name of DEIMOS alignment image ('last' for most recent)" }
string	logfile="get_seeing.log"	{ prompt="Name of logfile" }
bool	check=no	{ prompt="Run check_boxes for verification?" }
struct	*ilist 

begin
	string	i_image
    string  box_file    # box file
    string  slmsknam	# name of slitmask in use
	string	cursorfile	# file containing cursor input
	string	plotfile	# file containing graphics output input
	string	datafile	# file containing data
	string	parfile		# file containing parameters
	string	tmpprefix	# prefix for temp files
	string	host="deimos"
	string	subimage	# image extension to display
	real	xc,yc		# centroids
	real	fwhm_px
	real	fwhm_asec
	real	crval2
	int		frame=1
	int		n
	int		m
	int		k
	int		amp[8]
	int		istat

	# configure amp array...
	amp[1] = 2
	amp[2] = 1
	amp[3] = 4
	amp[4] = 3
	amp[5] = 6
	amp[6] = 5
	amp[7] = 8
	amp[8] = 7

	# prompt...
	i_image = image

	# declare temp files...
	tmpprefix = "tmp$get_seeing"
	cursorfile = mktemp( tmpprefix)
	plotfile = mktemp( tmpprefix)
	datafile = mktemp( tmpprefix)
	parfile = mktemp( tmpprefix)

	# translate last image...
	if( i_image == "last" )
		lastimage("-tail") | scan( i_image)

	# verify that the image exists...
	if( ! imaccess( i_image))
		error( 1, "Image "//i_image//" not found")
	print( "Processing image ", i_image)

	# read mask name from header...
    hselect( i_image//"[0]", "SLMSKNAM", "yes") | scan( slmsknam)
    if ( nscan() != 1 )
		error( 1, "Can't read SLMSKNAM from image header")

	box_file = "box." // slmsknam
	if( ! access( box_file))
	    get_boxes( i_image)

	# if box file not found, try using the most recent one...
	if( ! access( box_file)){
			ls("-1t","box.*") | scan( box_file)
		if( access( box_file)){
			beep()
			printf( "WARNING: using alternate box file %s\n", box_file)
		} else {
		    error( 1, "Could not generate box file "//box_file)
		}
	}

#	# execute check_boxes for alignment...
#	if ( check)
#		check_boxes( i_image, box_file, doxbox-)
#	else 
#		dmosdisplay( i_image, frame, zscale-, zra-, z1=1000, z2=10000)

	# store params...
	dpar( "rimexam", >parfile)
	rimexam.banner = yes
	rimexam.title = ""
	rimexam.xlabel = "Radius"
	rimexam.ylabel = "Pixel Value"
	rimexam.fitplot = yes
	rimexam.fittype = "gaussian"
	rimexam.center = yes
	rimexam.background = yes
	rimexam.radius = 5.
	rimexam.buffer = 5.
	rimexam.width = 5.
	rimexam.iterations = 1
	rimexam.xorder = 0
	rimexam.yorder = 0
	rimexam.magzero = 25.
	rimexam.beta = INDEF
	rimexam.rplot = 8.
	rimexam.x1 = INDEF
	rimexam.x2 = INDEF
	rimexam.y1 = INDEF
	rimexam.y2 = INDEF
	rimexam.pointmode = yes
	rimexam.marker = "plus"
	rimexam.szmarker = 1.
	rimexam.logx = no
	rimexam.logy = no
	rimexam.box = yes
	rimexam.ticklabels = yes
	rimexam.majrx = 5
	rimexam.minrx = 5
	rimexam.majry = 5
	rimexam.minry = 5
	rimexam.round = no

	# parse the file...
	ilist = box_file
	n = 0
	while( fscan( ilist, xc, yc) != EOF){
		n = n + 1

		# convert position in whole image to position in subimage...
		m = 1 + int(xc/1024.)
		k = amp[m]
		xc = mod(xc,1024)

		# correct y position for CCD windowing...
		subimage = i_image//"["//str(k)//"]"
		imgets( subimage, "CRVAL2")
		istat = fscan( imgets.value, crval2)
		yc = yc - crval2

		# correct x position for image flip...
		if ( mod(k,2) == 1 ) {
			xc = 1024 - xc
		}
		xc += 16

		print( xc, yc, " 100 r", >cursorfile)

		# examine file...
		printf( "Measuring box at (%d,%d) in image %s\n", xc, yc, subimage)
		display( subimage, frame, zsc-, zra-, z1=1000, z2=2500)
		printf( "running imexamine...\n")
		type( cursorfile)
		imexamine( input="", frame=frame, image="", defkey="r",
			imagecur=cursorfile, keeplog-, >>Gplotfile, >>datafile)
		delete( cursorfile, ver-)
	}


	# generate plot...
	gkiextract( plotfile, "1-99") \
		| gkimosaic( device="stdgraph", output="", nx=3, ny=2, 
			rotate=no, fill=yes, interactive=no)

	# print results...
	printf( "\nStar FHMW [px] FWHM [asec]\n")
	printf( "\n---- --------- -----------\n")
	tcalc( datafile, "c10", "c9*0.119")
	tprint( datafile, col="c9,c10", showhdr-)
	tstat( datafile, "c9", outtab="STDOUT", >"dev$null")
	fwhm_px = tstat.median
	tstat( datafile, "c10", outtab="STDOUT", >"dev$null")
	fwhm_asec = tstat.median
	printf( "%-8s %5.2f %5.2f\n", "Median", fwhm_px, fwhm_asec)

	# print to logfile...
	if( logfile != "")
		printf( "%-40s FWHM = %5.2f px = %5.2f asec\n", 
			i_image, fwhm_px, fwhm_asec, >>logfile)

	# clean up...
	cl( <parfile)
	delete( cursorfile, ver-)
	delete( plotfile, ver-)
	delete( datafile, ver-)
	delete( parfile, ver-)
	ilist=""
	
end
